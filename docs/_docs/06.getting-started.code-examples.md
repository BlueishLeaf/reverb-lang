---
title: "Code Examples"
permalink: /docs/getting-started/code-examples/
last_modified_at: 2020-03-15 00:00:00 +0000
toc: true
---
# Examples Without Sound
## Simple Map Function
```
var map = fn(array, f) begin
    var loop = fn(array, accumulated) begin
        if length(array) == 0 then
            return accumulated
        end
        return loop(tail(array), push(accumulated, f(head(array))))
    end
    return loop(array, [])
end

var nums = [1, 2, 3, 4, 5, 6, 7, 8, 9, 10]

var double = fn(x) begin x * 2 end
var square = fn(x) begin x * x end

print(map(nums, double))
print(map(nums, square))
```
## Simple Reduce Function
```
var reduce = fn(array, initial, f) begin
    var loop = fn(array, result) begin
        if (length(array) == 0) then
            return result
        end
        return loop(tail(array), f(result, head(array)))
    end
    return loop(array, initial)
end

var sum = fn(array) begin
    return reduce(tail(array), head(array), fn(initial, element) begin initial + element end)
end

var product = fn(array) begin
    return reduce(tail(array), head(array), fn(initial, element) begin initial * element end)
end

var nums = [1, 2, 3, 4, 5, 6, 7, 8, 9, 10]

print(sum(nums))
print(product(nums))
```
## Fibonacci Sequence
```
var fib = fn(n) begin
    if n == 1 then
        return 0
    end
    else if n == 2 then
        return 1
    end
    return fib(n - 1) + fib(n - 2)
end

var forLoop = fn(start, stop, func) begin
    var loop = fn(i, sequence) begin
        if i < stop then
            return loop(i + 1, push(sequence, func(i)))
        end
        return sequence
    end
    loop(start, [])
end

print(forLoop(2, 20, fibonacci))
```
# Examples With Sound
## The Difference Between Primes
```
# List of all the frequencies of the keys in a piano, in ascending order
var pianoKeys=[27.5,29.1353,30.8677,32.7032,34.6479,36.7081,38.8909,41.2035,43.6536,46.2493,48.9995,51.9130,55.0,58.2705,61.7345,65.4064,69.2957,73.4162,77.7817,82.4069,87.3071,92.4986,97.9989,103.826,110.0,116.541,123.471,130.813,138.591,146.832,155.563,164.814,174.614,184.997,195.998,207.652,220.0,233.082,246.942,261.626,277.183,293.665,311.127,329.628,349.228,369.994,391.995,415.305,440.0,466.164,493.883,523.251,554.365,587.33,622.254,659.255,698.456,739.989,783.991,830.609,880.0,932.328,987.767,1046.5,1108.73,1174.66,1244.51,1318.51,1396.91,1479.98,1567.98,1661.22,1760.0,1864.66,1975.53,2093.0,2217.46,2349.32,2489.02,2637.02,2793.83,2959.96,3135.96,3322.44,3520.0,3729.31,3951.07,4186.01]

# Check if a number is prime
var isPrime = fn(n, i) begin
    if n < 2 then return false end
    if n == 2 then return true end
    if i * i > n then return true end
    if n % i == 0 then return false end
    isPrime(n, i + 1)
end

# Return a list of the prime numbers between 'start' and 'stop'
var loopPrimes = fn(start, stop, f) begin
    var loop = fn(i, primes) begin
        if i < stop then
            if f(i, 2) then
                return loop(i + 1, push(primes, i))
            end
            return loop(i + 1, primes)
        end
        primes
    end
    loop(start, [])
end

# Iterate over primes and get the differences between them
var mapPrimes = fn(array, f) begin
    var loop = fn(array, accumulated) begin
        if length(array) == 1 then
            return accumulated
        end
        loop(tail(array), push(accumulated, f(head(array), tail(array)[0])))
    end
    loop(array, [])
end

# Map over the differences and make them positive
var mapDifferences = fn(array, f) begin
    var loop = fn(array, accumulated) begin
        if length(array) == 0 then
            return accumulated
        end
        loop(tail(array), push(accumulated, f(head(array))))
    end
    loop(array, [])
end

# Map over the sequence and create a list of musical strings using the piano key list
var mapSequence = fn(array, f) begin
    var loop = fn(array, accumulated) begin
        if length(array) == 0 then
            return accumulated
        end
        loop(tail(array), push(accumulated, f(pianoKeys[head(array) % 88] * 12, 0.9, 200)))
    end
    loop(array, [])
end

var primes = loopPrimes(1, 100, isPrime)
var differences = mapPrimes(primes, fn(x, y) begin x - y end)
var positiveDifferences = mapDifferences(differences, fn(x) begin x * -1 end)
print(positiveDifferences)
var sequence = mapSequence(positiveDifferences, string)
play(sequence)
```
## Clair De Lune
```
# Converts a treble clef note to a Karplus Strong string
var notesT = fn(quaverCount, notes) begin
    var loop = fn(array, accumulated) begin
        if length(array) == 0 then
            return accumulated
        end
        var midi = trebleMidi[head(array) + 8] + trebleKeys[head(array) + 8]
        var freq = midiToHz(midi)
        loop(tail(array), push(accumulated, string(freq, 0.9, quaverCount * q)))
    end
    overlay(loop(notes, []))
end

var notesTQRun = fn(array) begin
    var loop = fn(array, accumulated) begin
        if length(array) == 0 then
            return accumulated
        end
        var midi = trebleMidi[head(array) + 8] + trebleKeys[head(array) + 8]
        var freq = midiToHz(midi)
        loop(tail(array), push(accumulated, string(freq, 0.9, q)))
    end
    concatinate(loop(array, []))
end

var trebleMidi = [57, 59, 60, 62, 64, 65, 67, 69, 71, 72, 74, 76, 77, 79, 81, 83, 84]
var trebleKeys = [-1, -1, 00, -1, -1, 00, -1, -1, -1, 00, -1, -1, 00, -1, -1, -1, 00]
var finalNoteLength = 9
var q = 309

var rhA = concatinate(notesT(7, [1]), notesTQRun([0, 1, 0, 3, 0, -1, 0]), notesT(2, [-1]), notesTQRun([-2, -1]), notesT(3, [-2]), notesT(finalNoteLength, [-3]))
var rhB = concatinate(notesT(6, [-1]), notesT(6, [-2]), notesT(3, [-2]), notesT(6, [-4]), notesT(finalNoteLength, [-4]))

var couplets = overlay(concatinate(notesT(1, [2]), notesT(3, [4]), notesT(3, [2])), notesT(7, [0]))

var rightHand = concatinate(silence(q * 2), notesT(4, [4, 6]), notesT(4, [2, 4]), notesT(1, [1, 3]), notesT(1, [2, 4]), notesT(7, [1, 3]), notesT(1, [0, 2]), notesT(1, [1, 3]), couplets, notesT(1, [-1, 1]), notesT(1, [0, 2]), concatinate(rhA, rhB))
var leftHand = concatinate(silence(q * 1), notesT(8, [-1, -3]), notesT(9, [0, -2]), notesT(9, [-1, -3]), notesT(9, [-2, -4]), notesT(6, [-4, -5]), notesT(3, [-4, -6]), notesT(6, [-5, -7]), notesT(finalNoteLength, [-6, -8]))

# Overlay the left hand on top of the right hand
var clairDeLune = overlay(leftHand, rightHand)

# Play the piece
play(clairDeLune)
```